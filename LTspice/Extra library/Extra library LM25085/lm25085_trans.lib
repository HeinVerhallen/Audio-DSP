* PSpice Model Editor - Version 16.2.0
*$
* LM25085
*****************************************************************************
*  (C) Copyright 2012 Texas Instruments Incorporated. All rights reserved.
*****************************************************************************
** This model is designed as an aid for customers of Texas Instruments.
** TI and its licensors and suppliers make no warranties, either expressed
** or implied, with respect to this model, including the warranties of 
** merchantability or fitness for a particular purpose.  The model is
** provided solely on an "as is" basis.  The entire risk as to its quality
** and performance is with the customer
*****************************************************************************
*
** Released by: WEBENCH Design Center, Texas Instruments Inc.
* Part: LM25085
* Date: 17JAN2013
* Model Type: Transient
* Simulator: PSPICE
* Simulator Version: 16.2.0.s003
* EVM Order Number: AN-1905 
* EVM Users Guide: SNVA375A,12 Nov 2008
* Datasheet: SNVS593G,07 May 2012 
*
* Model Version: Final 1.00
*
******************************************************************************************
*
* Updates:
*
* Final 1.00   
* Release to Web.
*
******************************************************************************************
.SUBCKT LM25085_TRANS IADJ RON FB GND ISEN PGATE VCC VIN
*FAMILY ANALOG ANALOG ANALOG ANALOG ANALOG ANALOG ANALOG ANALOG
VL 1 GND
X2 COMP INVCOMP INVB_0
EBREF1 REF1 GND VALUE={IF(V(ENON)>1,1.25,0)}
X5 INVCOMP ONT ONCOMP AND2B_0
RDUMMY PGATE 111 50
EB4 111 GND VALUE={IF(V(DRIVE)>1,V(VIN),V(VCC))}
XOFF CAPOFF REF OFFT QLATCH COMPWDELEN_0 
X22 INVCOMP 49 DRVNOT DRIVE LATCHNAND_0 
EBREF REF GND VALUE={IF(V(VCC)>1.25,1.25,V(VCC))}
SSD V5V ENON RON GND _S12_MOD
.MODEL _S12_MOD vswitch VON=0.775 VOFF=0.685
EBENVCC 24 GND VALUE={IF(V(ENON)<1,5,0)}
R58 ENON GND 1K
X8 QLATCH OFFRESET INVB_0 
X6 INVCL 54 QLATCH QN LATCHNAND_0 
EB7 VIN VCC VALUE={IF(V(VIN)<8.5,V(VIN),7.7)}
SEV VCC GND 24 GND _S1_MOD
.MODEL _S1_MOD vswitch VT=1 RON=1K
XA1 QLATCH CL 50 NAND2A1
S1 V5V ENINT RON GND _S1_MOD
R20 OFFT 28 10
C11 28 GND 10P
S2 ENINT VCCUVLO VIN VCC _S2_MOD
.MODEL _S2_MOD vswitch VT=5 VH=200M
R1X GND VCCUVLO 1K
I_ILIM IADJ GND DC= 42u
XA2  HI 50 IFAULTCLEAR NAND2A2
EBFB 15 GND VALUE={IF(V(SS)<V(REF1), V(SS),V(REF1))}
R17 GND ENINT 1K
V_V5V V5V GND DC=5
CON 7 GND 12P
GBTON GND 7 VALUE = {(12P*1.25*I(V17)*(V(VIN)-1.56)/(145E-12*((V(VIN)-V(RON)
+ )+I(V17)*1400)+50E-9*I(V17)*(V(VIN)-1.56)))}
V17 RON 22
X7 7 GND DRIVE GND SSWITCH_0 
X69 7 REF ONT OFFRESET COMPWDELEN_1
DON1 22 4 _D3_MODX
.MODEL _D3_MODX D IS=1.0E-13 N=1 EG=1.11 M=0.5
DON2 4 46 _D
.MODEL _D D IS=1.0E-13 N=1
RONIN 46 GND 500
COFF CAPOFF GND 5.004P
XFB 15 FB COMP COMPWDEL_0 
XCL IADJ ISEN CL COMPWDEL_0 
EBOVER OV GND VALUE={IF(V(FB)>1.6,5,0)}
I_BTOFF GND CAPOFF DC=15u
XSOFF CAPOFF GND OFFRESET GND SSWITCH_0 
CSS SS GND 30N IC=0
MSS SS VSDN GND GND _M
.MODEL _M NMOS RD=20K VTO=1
EBSSEN VSDN GND  VALUE={IF (V(ENON)<1,5,0)}
QSS1 45 45 SSV QP
.MODEL QP PNP
QSS2 SS 45 SSV QP
R63 45 1 200K
X66 LO OV ONCOMP QLATCH 49 NOR4B_0 
X67 HI LO INVB_0 
VNOFAULTS HI 0 DC=5
X68 CL INVCL INVB_0 
X71 LO 28 54 NOR2B_0 
EBSSV SSV GND VALUE={IF(V(VIN)-V(VCC) -0.26>3.8,3.8,V(VIN)-V(VCC))}
.ENDS
*$
********************************************************************************
*********Subcircuit for INVERTER INVB0***************
*******************************************************************************
.SUBCKT INVB_0 1  2
*           IN OUT
EB1 3 0 VALUE={IF(V(1)>3,0.1,IF(V(1)<0.6,5,0.1))}
RD 3 2 1
CD 2 0 0.087NF
.ENDS
*$
******************************************************
******Subcircuit for AND2B**********
******************************************************
.SUBCKT AND2B_0 1 2 3 
*            A B OUT
EB1 4 0 VALUE={IF((V(1)>3 & V(2)>3),5,0.1)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
*******************************************************
********Subcircuit for COMPWDELEN0**********
********************************************************
.SUBCKT COMPWDELEN_0 1 2 3 EN
*               + - OUT
EB1 4 0 VALUE={IF(V(EN)<1,0,IF(V(1)>V(2),5,0))}
T1 4 0 5 0 Z0=1 TD=100.000N
R1 5 0 1
EB2 3 0 VALUE={IF(V(5)<0,0,V(5))}
.ENDS
*$
*****************************************************************
********Subcircuit for COMPWDELEN1************
******************************************************************
.SUBCKT COMPWDELEN_1 1 2 3 EN
*               + - OUT
*B1 4 0 V=V(EN) < 1 ? 0 : V(1) > V(2) ? 5 : 0
EB1 4 0 VALUE={IF(V(EN)<1,0,IF(V(1)>V(2),5,0))}
T1 4 0 5 0 Z0=1 TD=75.000N
R1 5 0 1
*B2 3 0 V=V(5) < 0 ? 0 : V(5)
EB2 3 0 VALUE={IF(V(5)<0,0,V(5))}
.ENDS
*$
****************************************************************
*******Subcircuit for Latch Nand************
****************************************************************
.SUBCKT LATCHNAND_0  3 4 1 2
*                  S R Q QN
X1 3 2 1 NAND2B_0
X2 4 1 2 NAND2B_0
.ENDS
*$
*****************************************************
******** Subcircuit for NAND2A1****************
*****************************************************
.SUBCKT NAND2A1 1 2 3
*             A B OUT
EB1 4 0 VALUE ={IF((V(1)<0.6 & V(2)<0.6),5,0)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
*****************************************************
******** Subcircuit for NAND2A2****************
*****************************************************
.SUBCKT NAND2A2 1 2 3
*             A B OUT
EB1 4 0 VALUE ={IF((V(1)<0.6 & V(2)<0.6),5,0)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
*******************************************************
*****************************************************
******** Subcircuit for NAND2B****************
*****************************************************
.SUBCKT NAND2B_0 1 2 3
*             A B OUT
EB1 4 0 VALUE ={IF((V(1)<0.6 | V(2)<0.6),5,0)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
*******************************************************
******Subcircuit for Switch******************
*******************************************************
.SUBCKT SSWITCH_0 1 2 3   4
*CONNECTIONS    + - NC+ NC-
*PARAMETERS: RON ON RESISTANCE IN OHMS, ROFF OFF RESISTANCE IN OHMS
* VON ON CURRENT IN AMPS, VOFF OFF CURRENT IN AMPS
* IF V(3,4) > VON THEN RSWITCH=RON, IF V(3,4) < VOFF THEN RSWITCH=ROFF, ELSE
* RSWITCH WILL SMOOTHLY TRANSISTION BETWEEN THE ON AND OFF STATES
SA1 1 2 3 4 SMOOTH
.MODEL SMOOTH VSWITCH RON=100.000M ROFF=10.0000MEG VON=2.0000  VOFF=0
.ENDS
*$
***********************************************************
**********Subcircuit for COMPWDEL**************
***********************************************************
.SUBCKT COMPWDEL_0 1 2 3 PARAMS: TD=100N
*               + - OUT
EB1 4 0 VALUE={IF(V(1)>V(2),5,0)}
T1 4 0 5 0 Z0=1 TD={TD}
R1 5 0 1
EB2 3 0 VALUE={IF(V(5)<0,0,V(5))}
.ENDS
*$
*********************************************************************
************ Subcircuit for NOR4B*****************
**********************************************************************
.SUBCKT NOR4B_0 1 2 3 4 5 
*            A B C D OUT
*B1 6 0 V= ~(V(1)|V(2)|V(3)|V(4))
EB1 6 0 VALUE = {IF((V(1)<0.6 & V(2) <0.6 & V(3)<0.6 & V(4)
+<0.6),5,0.1)}
RD 6 5 1
CD 5 0 0.087NF
.ENDS
*$
**************************************
***** Sub Circuit for 2-NOR *******
**************************************
.SUBCKT NOR2B_0 1 2 3 PARAMS: VHIGH = 5 VLOW = 0 VTHLOW = 0.6 VTHHIGH = 3
*            A B OUT
EB1 4 0 VALUE = {IF((V(1)<VTHLOW & V(2)<VTHLOW),VHIGH,VLOW)}
RD 4 3 1
CD 3 0 0.087NF
.ENDS
*$
* created using Model Editor release 16.2.0 on 12/22/12 at 17:50
*DEVICE=MBRB2060CT,D
.subckt MBRB2060CT AN CAT 
+ params:
+ AREA=1.0
+ IS=9e-6
+ RS=60e-3
+ N=1.442
+ CJO=470e-12
+ M=0.8
+ EG=0.69
+ XTI=2
+ BV=200
+ IBV=1e-6
D_MBRB2060CT AN CAT model22 {area} 
.model model22 d
+ is={IS}
+ rs={RS}
+ n={N}
+ cjo={CJO}
+ m={M}
+ eg={EG}
+ xti={XTI}
+ bv={BV}
+ ibv={IBV}
.ends MBRB2060CT
*$
******************************************************************************************
* WB_POWER_P_MOSFET SPICE MODEL
*
******************************************************************************************
* GENERIC 60V, 3A P-CHANNEL MOSFET MODEL
* MODIFIED FAIRCHILD FDC5614P MODEL
.SUBCKT P_MOSFET 20 10 30
*               TERMINALS:  D  G  S
* 20=DRAIN 10=GATE 30=SOURCE 50=VTEMP
V50 50 0 27
RG 10 11X 1
RDU 12X 1 1U
M1 2 1 4X 4X DMOS L=1U W=1U
.MODEL DMOS PMOS(VTO=-2 KP=13
+THETA=0.1 VMAX=3E5 LEVEL=3)
CGS 1 5X 750P
RD 20 4 3.2E-2
DDS 4 5X DDS
.MODEL DDS D(M=4.22E-1 VJ=7.65E-1 CJO=253P)
DBODY 20 5X DBODY
.MODEL DBODY D(IS=4.7E-12 N=1.116224 RS=0.000695 TT=30.4N)
RA 4 2 3.2E-2
RS 5X 5 0.5M
LS 5 30 0.5N
M2 1 8 6 6 INTER
E2 8 6 4 1 2
.MODEL INTER PMOS(VTO=0 KP=10 LEVEL=1)
CGDMAX 7 4 652P
RCGD 7 4 10MEG
DGD 4 6 DGD
RDGD 4 6 10MEG
.MODEL DGD D(M=3.2E-1 VJ=7.7E-3 CJO=652P)
M3 7 9 1 1 INTER
E3 9 1 4 1 -2
*ZX SECTION
EOUT 4X 6X POLY(2) (1X,0) (3X,0) 0 0 0 0 1
FCOPY 0 3X VSENSE 1
RIN 1X 0 1G
VSENSE 6X 5X 0
RREF 3X 0 10M
E_BED 101 0 Value ={V(50,100)}
VAMB 100 0 25
EKP 1X 0 101 0 .042
*VTO SECTION
EVTO 102 0 101 0 .004
EVT 11X 12X 102 0 1
EBL VB1 VB2 101 0  0.8
VBLK VB2 0 60
D DB1 20 DBLK
.MODEL DBLK D(IS=1E-14 CJO=.1P RS=.1)
EDB 0 DB1 VB1 0 1
.ENDS
*$
******************************************************************************************
* WB_CAP_TANTALUM SPICE MODEL
*
******************************************************************************************
* INPUTS: IC
*
.SUBCKT COUT 13 2 PARAMS: IC = 0
C3 13 11 4.2580725000000006E-6 IC= {IC}
C4 13 3 2.1290362500000003E-6 IC={IC}
C5 13 4 1.0645181250000001E-6 IC={IC}
R1 4 5 0.4375
R2 3 6 0.315
R3 11 7 0.6124999999999999
R4 12 8 0.8119999999999999
C6 13 9 1.7032290000000002E-5 IC={IC}
R5 13 0 1000000.0
R6 9 10 2.1875
L1 5 2 8.0E-9
L2 6 2 8.0E-9
L3 7 2 8.0E-9
L4 8 2 8.0E-9
L5 10 2 8.0E-9
R8 2 0 1000000.0
C2 13 12 8.516145000000001E-6 IC={IC}
.ENDS
*$
******************************************************************************************
* INDUCTOR MODEL
*
******************************************************************************************
.subckt LDCR 1 2 PARAMS: L = 10u IC = 0 DCR = 0.06
L1 2 3 {L} IC = {IC}
RDCR 3 1 {DCR}
.ENDS
*$
******************************************************************************************
* INPUT CAPACITOR
******************************************************************************************
* INPUTS: IC, ESR, ESL, RLEAK, C
*
.SUBCKT CIN 1 2
+ PARAMs: C_cap = 2u ESR = 0.0055 IC = 0
* C = 2.0E-6 F
* ESR = 0.0055 Ohm
Ccap 1 3 {C_cap} IC = {IC} 
Resr 3 2 {ESR}
.ENDS
*$
